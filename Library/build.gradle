apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.github.xm3366336'

android {
    namespace 'com.pengl.record'
    compileSdkVersion = rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        ndkVersion rootProject.ndkVersion

        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    packagingOptions {
        jniLibs {
            pickFirsts += ['lib/armeabi-v7a/libmp3lame.so', 'lib/arm64-v8a/libmp3lame.so', 'lib/armeabi-v7a/libmp3recorder-lib.so', 'lib/arm64-v8a/libmp3recorder-lib.so']
        }
    }

    // 避免重复导入库

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}
